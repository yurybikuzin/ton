#!/usr/bin/fift -s
"TonUtil.fif" include

{ ."usage: " @' $0 type ." <filename-base> <seqno> <new-onwer-pk> [<savefile>]" cr
  ."Creates a request to DNS Resolver smart contract requesting to change public key (i.e. transfer to new owner), "
  ."with private key loaded from file <filename-base>.pk, "
  ."and saves it into <savefile>.boc ('<filename-base>-transfer.boc' by default)" cr 1 halt
} : usage
$# dup 2 < swap 4 > or ' usage if

$1 constant file-base
$2 parse-int constant seqno
$3 constant new-onwer-pk-file-base
def? $4 { @' $4 } { file-base +"-transfer" } cond constant savefile

file-base +".addr" load-address
2dup 2constant smc_addr
."DNS Resolver smart contract address = " 2dup .addr cr 6 .Addr cr
file-base +".pk" load-keypair nip constant config_pk

<b
  new-onwer-pk-file-base file>B dup Blen 32 <> abort"Private key must be exactly 32 bytes long"
  priv>pub B,
b>
<b
  seqno 32 u,
  0x50624b21 32 u,
  swap ref,
b>
dup ."signing message: " <s csr. cr
dup hashu config_pk ed25519_sign_uint
<b b{1000100} s, smc_addr addr, 0 Gram, b{00} s,
   swap B, swap <s s, b>
dup ."resulting external message: " <s csr. cr
2 boc+>B dup Bx. cr
savefile +".boc" tuck B>file
."(Saved to file " type .")" cr
