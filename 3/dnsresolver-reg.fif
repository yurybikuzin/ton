#!/usr/bin/fift -s
"TonUtil.fif" include

{ ."usage: " @' $0 type ." <filename-base> <seqno> <subdomain> <category> <subdomain-addr> [<savefile>]" cr
  ."Creates a request to DNS Resolver smart contract requesting to reg new subdomain, "
  ."with private key loaded from file <filename-base>.pk, "
  ."and saves it into <savefile>.boc ('<filename-base>-reg.boc' by default)" cr 1 halt
} : usage
$# dup 5 < swap 6 > or ' usage if

$1 constant file-base
$2 parse-int constant seqno
$3 constant subdomain
$4 parse-int constant category
$5 $>smca not abort"invalid smart-contract address" constant subdomain-addr
def? $6 { @' $6 } { file-base +"-reg" } cond constant savefile

file-base +".addr" load-address
2dup 2constant smc_addr
."DNS Resolver smart contract address = " 2dup .addr cr 6 .Addr cr
file-base +".pk" load-keypair nip constant config_pk

<b
  category 16 u,
  subdomain-addr 256 u,
b>
<b subdomain $, b>
<b
  seqno 32 u,
  0x43665021 32 u,
  swap ref,
  swap ref,
b>
dup <s csr.
dup ."signing message: " <s csr. cr
dup hashu config_pk ed25519_sign_uint
<b b{1000100} s, smc_addr addr, 0 Gram, b{00} s,
   swap B, swap <s s, b>
dup ."resulting external message: " <s csr. cr
2 boc+>B dup Bx. cr
savefile +".boc" tuck B>file
."(Saved to file " type .")" cr
