#!/usr/bin/fift -s
"TonUtil.fif" include
"Asm.fif" include

{ ."usage: " @' $0 type ." <filename-base> <seqno> [<savefile>]" cr
  ."Creates a request to DNS Resolver smart contract requesting to change smart contract code to the one currently stored in auto/dnsresolver-updated.fif, "
  ."with private key loaded from file <filename-base>.pk, "
  ."and saves it into <savefile>.boc ('<filename-base>-upd.boc' by default)" cr 1 halt
} : usage
$# dup 2 < swap 3 > or ' usage if

"auto/dnsresolver-updated.fif" include constant include-file

$1 constant file-base
$2 parse-int constant seqno
def? $3 { @' $3 } { file-base +"-upd" } cond constant savefile

file-base +".addr" load-address
2dup 2constant smc_addr
."DNS Resolver smart contract address = " 2dup .addr cr 6 .Addr cr
file-base +".pk" load-keypair nip constant config_pk

// "auto/updated-pingpong.fif" include // this line here leads to error, so include-file introduced above
include-file
dup <s csr. cr

<b
  seqno 32 u,
  0x4e436f64 32 u,
  swap ref,
b>
dup ."signing message: " <s csr. cr
dup hashu config_pk ed25519_sign_uint
<b b{1000100} s, smc_addr addr, 0 Gram, b{00} s,
   swap B, swap <s s, b>
dup ."resulting external message: " <s csr. cr
2 boc+>B dup Bx. cr
savefile +".boc" tuck B>file
."(Saved to file " type .")" cr
